cmake_minimum_required(VERSION 3.12.0)

project(cameo-api-cpp
  VERSION 1.1.0
  LANGUAGES CXX
  HOMEPAGE_URL "https://code.ill.fr/cameo/cameo"
  )
# name of the target library
set(LIBNAME ${PROJECT_NAME})
set(NAMESPACE Cameo)

#--------------- Options
option(TESTS "Compiling the test programs" OFF)
if(NOT DEFINED ${BUILD_SHARED_LIBS})
  option(BUILD_SHARED_LIBS "Building dynamic (ON) or static (OFF)" ON)
  message(STATUS "Building shared libraries: ${BUILD_SHARED_LIBS}")
endif()
if (DEFINED CAMEO_API_CPP_SHARED_LIBS)
    set(BUILD_SHARED_LIBS "${CAMEO_API_CPP_SHARED_LIBS}")
endif ()

#--------------- Helpers
include(GNUInstallDirs)
include(CMakePackageConfigHelpers)
include(FetchContent)


if(NOT DEFINED ${CMAKE_BUILD_TYPE})
  set(CMAKE_BUILD_TYPE "Release")
endif()
message(STATUS "Build type: ${CMAKE_BUILD_TYPE}")

# fix the RPATH for the linker
if(NOT APPLE)
  set(CMAKE_INSTALL_RPATH $ORIGIN)
endif()

#------------------------------------------------------------
# Dependencies
#------------------------------------------------------------
list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR})
find_package(ZeroMQ REQUIRED)
find_package (Threads REQUIRED)
find_package(Rapidjson 1.1.0 QUIET)
if(NOT Rapidjson_FOUND)
  message(WARNING "Using Rapidjson from GIT repository")
  FetchContent_Declare(rapidjson
	GIT_REPOSITORY https://github.com/Tencent/rapidjson.git
	GIT_TAG v1.1.0
	GIT_SHALLOW True
	)
  FetchContent_GetProperties(rapidjson)
  FetchContent_Populate(rapidjson)

  set(Rapidjson_ROOT ${rapidjson_SOURCE_DIR})
  find_package(Rapidjson REQUIRED)
endif()
message(STATUS "Rapidjson found here: ${Rapidjson_INCLUDE_DIRS}")
#------------------------------------------------------------
# Libraries
#------------------------------------------------------------
set (INSTALL_INCLUDEDIR ${CMAKE_INSTALL_INCLUDEDIR}/cameo/api)
#---------------
# Files
file(GLOB_RECURSE CAMEO_SOURCE_FILES "src/*.cpp")
configure_file(include/cameo.h.in ${INSTALL_INCLUDEDIR}/cameo.h )

add_library(${LIBNAME}-obj OBJECT ${CAMEO_SOURCE_FILES})
add_library(${LIBNAME}-static STATIC $<TARGET_OBJECTS:${LIBNAME}-obj>)
add_library(${LIBNAME} ${CAMEO_SOURCE_FILES})
add_library(${NAMESPACE}::${LIBNAME} ALIAS ${LIBNAME})
target_link_libraries(${LIBNAME} PUBLIC zmq Threads::Threads)
target_include_directories(${LIBNAME}
  PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/src/ # all the private headers
  PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
  PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}/include>  # here's the version
  PUBLIC $<INSTALL_INTERFACE:${INSTALL_INCLUDEDIR}>
  )
target_include_directories(${LIBNAME}
  SYSTEM PRIVATE ${Rapidjson_INCLUDE_DIRS}/
)
target_compile_features(${LIBNAME} PUBLIC cxx_std_17)

set_target_properties(${LIBNAME} PROPERTIES
  SOVERSION ${PROJECT_VERSION_MAJOR}
  VERSION ${PROJECT_VERSION}
  POSITION_INDEPENDENT_CODE 1 # this is needed for static compilation for cameo-api-python
  )

#------------------------------------------------------------
# Install
#------------------------------------------------------------
install(FILES ${CMAKE_CURRENT_SOURCE_DIR}/LICENSE.txt
  DESTINATION ${CMAKE_INSTALL_DOCDIR}/
  RENAME copyright
  COMPONENT ${PROJECT_NAME}_Runtime
  )



#---------------
# Library version
write_basic_package_version_file(${PROJECT_NAME}ConfigVersion.cmake
  COMPATIBILITY SameMajorVersion
  VERSION ${PROJECT_VERSION}
  )

install(TARGETS ${LIBNAME}
  EXPORT cameoTargets
  RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR} # windows
    COMPONENT ${PROJECT_NAME}_Runtime
  LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}/cameo/ # dynamic
    COMPONENT ${PROJECT_NAME}_Runtime
    NAMELINK_COMPONENT ${PROJECT_NAME}_Development #from CMake 3.12
  ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}/cameo/ # static
    COMPONENT ${PROJECT_NAME}_Development
  PUBLIC_HEADER  DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
    COMPONENT ${PROJECT_NAME}_Development
  INCLUDES DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
    COMPONENT ${PROJECT_NAME}_Development
  )

# this is to install the headers
install(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/include/ # don't forget the trailing /
  DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/cameo/api/
  COMPONENT ${PROJECT_NAME}_Development
  FILES_MATCHING  PATTERN *.h
  )
# this is to install the version header that is generated by cmake
install(DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/include/ # don't forget the trailing /
  DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/cameo/api/
  COMPONENT ${PROJECT_NAME}_Development
  FILES_MATCHING  PATTERN *.h
  )


export(EXPORT cameoTargets
  #   FILE ${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}-${type}-targets.cmake
  FILE ${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}-targets.cmake
  NAMESPACE ${NAMESPACE}::
  )
configure_file(cmake/${PROJECT_NAME}Config.cmake
  ${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}Config.cmake
  COPYONLY
  )

set(ConfigPackageLocation ${CMAKE_INSTALL_LIBDIR}/cmake)
install(EXPORT cameoTargets
  FILE ${PROJECT_NAME}-targets.cmake
#  FILE ${PROJECT_NAME}Config.cmake #${PROJECT_NAME}Targets.cmake
  NAMESPACE ${NAMESPACE}::
  DESTINATION ${ConfigPackageLocation}
  COMPONENT ${PROJECT_NAME}_Development
  )
install(
  FILES
  ${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}Config.cmake
  ${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}ConfigVersion.cmake
  DESTINATION ${ConfigPackageLocation}
  COMPONENT ${PROJECT_NAME}_Development
  )
# This makes the project importable from the install directory
# Put config file in per-project dir (name MUST match), can also
# just go into 'cmake'.

if(UNIX)
  add_custom_command(
    OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/changelog.gz
    COMMAND gzip -cn9 ${CMAKE_CURRENT_SOURCE_DIR}/ChangeLog ${CMAKE_CURRENT_BINARY_DIR}/changelog.gz
    WORKING_DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}"
    DEPENDS "${CMAKE_CURRENT_SOURCE_DIR}/changelog"
    COMMENT "Compressing changelog"
	)
  
  add_custom_target(changelog
	ALL
	#	DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/changelog.gz
	COMMAND gzip -cn9 ${CMAKE_CURRENT_SOURCE_DIR}/ChangeLog > ${CMAKE_CURRENT_BINARY_DIR}/changelog.gz
	)
  
  install(FILES "${CMAKE_CURRENT_BINARY_DIR}/changelog.gz"
	DESTINATION "${CMAKE_INSTALL_DOCDIR}"
	COMPONENT ${PROJECT_NAME}_Runtime
	)
endif(UNIX)

#------------------------------------------------------------
# Doc
#------------------------------------------------------------
find_package(Doxygen QUIET)
if (DOXYGEN_FOUND)
  # set input and output files
  set(DOXYGEN_IN ${CMAKE_CURRENT_SOURCE_DIR}/Doxyfile.in)
  set(DOXYGEN_OUT ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile)
  
  # request to configure the file
  configure_file(${DOXYGEN_IN} ${DOXYGEN_OUT} @ONLY)
  message("Doxygen build started")
  
  # note the option ALL which allows to build the docs together with the application
  add_custom_target( cameo_doc 
    COMMAND ${DOXYGEN_EXECUTABLE} ${DOXYGEN_OUT}
    WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
    COMMENT "Generating API documentation with Doxygen"
    VERBATIM )
else (DOXYGEN_FOUND)
  message("Doxygen need to be installed to generate the doxygen documentation")
endif (DOXYGEN_FOUND)

#------------------------------------------------------------
# CPack
#------------------------------------------------------------
include(CPack)

#------------------------------------------------------------
# Tests
#------------------------------------------------------------
if(TESTS)
# Note: execute ctest --verbose to have more details 
enable_testing()
add_subdirectory(tests)
endif(TESTS)
