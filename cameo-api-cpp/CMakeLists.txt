if(NOT DEFINED PROJECT_NAME)
    cmake_minimum_required(VERSION 3.12.0)
endif()  

project(cameo-api-cpp
  VERSION 1.1.0
  LANGUAGES CXX
  HOMEPAGE_URL "https://code.ill.fr/cameo/cameo"
  )

option(TESTS "Compiling the test programs" OFF)

message("========================================")
message("Project ${PROJECT_NAME},\n  major version: ${PROJECT_VERSION_MAJOR},\n  minor version: ${PROJECT_VERSION_MINOR},\n  minor version: ${PROJECT_VERSION_PATCH},\n  version ${PROJECT_VERSION}")


include(GNUInstallDirs)
include(CMakePackageConfigHelpers)
include(FetchContent)

if(NOT DEFINED ${BUILD_SHARED_LIBS})
  option(BUILD_SHARED_LIBS "Building dynamic (ON) or static (OFF)" ON)
endif()

if(NOT DEFINED ${CMAKE_BUILD_TYPE})
  set(CMAKE_BUILD_TYPE "Release")
endif()
message(${CMAKE_BUILD_TYPE})

# fix the RPATH for the linker
#if(not APPLE)
#  set(CMAKE_INSTALL_RPATH $ORIGIN)
#endif()

#------------------------------------------------------------
# Dependencies
#------------------------------------------------------------
list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR})
find_package(ZeroMQ REQUIRED)
find_package (Threads REQUIRED)

find_package(Rapidjson QUIET)
if(NOT Rapidjson_FOUND)
  message(WARNING "Using Rapidjson from GIT repository")
  FetchContent_Declare(rapidjson
	GIT_REPOSITORY https://github.com/Tencent/rapidjson.git
	GIT_TAG v1.1.0
	GIT_SHALLOW True
	)
  FetchContent_GetProperties(rapidjson)
  FetchContent_Populate(rapidjson)

  set(Rapidjson_ROOT ${rapidjson_SOURCE_DIR})
  find_package(Rapidjson REQUIRED)
endif()
  message(${Rapidjson_INCLUDE_DIRS})
#------------------------------------------------------------
# Libraries
#------------------------------------------------------------
set (INSTALL_INCLUDEDIR ${CMAKE_INSTALL_INCLUDEDIR}/cameo/api)
#---------------
# Files
file(GLOB_RECURSE CAMEO_SOURCE_FILES "src/*.cpp")
configure_file(include/cameo.h.in include/cameo.h )

set (LIBNAME ${PROJECT_NAME})

add_library(${LIBNAME} ${CAMEO_SOURCE_FILES})
target_link_libraries(${LIBNAME} PUBLIC zmq Threads::Threads)
target_include_directories(${LIBNAME}
  PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/src/ # all the private headers
  PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
  PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}/include>  # here's the version
  PUBLIC $<INSTALL_INTERFACE:${INSTALL_INCLUDEDIR}>
  )
target_include_directories(${LIBNAME}
  SYSTEM PRIVATE ${Rapidjson_INCLUDE_DIRS}/
)
target_compile_features(${LIBNAME} PUBLIC cxx_std_17)

set_target_properties(${LIBNAME} PROPERTIES
  SOVERSION ${PROJECT_VERSION_MAJOR}
  VERSION ${PROJECT_VERSION}
  )



#------------------------------------------------------------
# Install
#------------------------------------------------------------
install(FILES ${CMAKE_SOURCE_DIR}/LICENSE.txt
  DESTINATION ${CMAKE_INSTALL_DOCDIR}/
  RENAME copyright
  COMPONENT lib
  )

# this is to install the headers
install(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/include/ # don't forget the trailing /
  DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/cameo/api/
  COMPONENT dev
  FILES_MATCHING  PATTERN *.h
  )
# this is to install the version header that is generated by cmake
install(DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/include/ # don't forget the trailing /
  DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/cameo/api/
  COMPONENT dev
  FILES_MATCHING  PATTERN *.h
  )


# in CMake 3.14 you just need #install(TARGETS cameo)
install(TARGETS ${LIBNAME}
  EXPORT cameoexport
  RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR} # windows
    COMPONENT lib
  LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}/cameo/ # dynamic
    COMPONENT lib
    NAMELINK_COMPONENT dev #from CMake 3.12
  ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}/cameo/ # static
    COMPONENT dev
  PUBLIC_HEADER  DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
    COMPONENT dev
  INCLUDES DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
    COMPONENT dev
  )


# This makes the project importable from the install directory
# Put config file in per-project dir (name MUST match), can also
# just go into 'cmake'.
install(EXPORT cameoexport
  DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake
  FILE ${PROJECT_NAME}Config.cmake
  COMPONENT dev
  NAMESPACE Cameo::
  )
#---------------
# Library version
write_basic_package_version_file(${PROJECT_NAME}ConfigVersion.cmake
  COMPATIBILITY SameMajorVersion
  VERSION ${PROJECT_VERSION}
  )



# makes the project importable from the build directory
export(TARGETS ${LIBNAME} FILE ${CMAKE_BINARY_DIR}/${PROJECT_NAME}Config.cmake)


################ Doc
find_package(Doxygen)
if (DOXYGEN_FOUND)
  # set input and output files
  set(DOXYGEN_IN ${CMAKE_CURRENT_SOURCE_DIR}/Doxyfile.in)
  set(DOXYGEN_OUT ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile)
  
  # request to configure the file
  configure_file(${DOXYGEN_IN} ${DOXYGEN_OUT} @ONLY)
  message("Doxygen build started")
  
  # note the option ALL which allows to build the docs together with the application
  add_custom_target( cameo_doc 
    COMMAND ${DOXYGEN_EXECUTABLE} ${DOXYGEN_OUT}
    WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
    COMMENT "Generating API documentation with Doxygen"
    VERBATIM )
else (DOXYGEN_FOUND)
  message("Doxygen need to be installed to generate the doxygen documentation")
endif (DOXYGEN_FOUND)


######

set (CPACK_PACKAGE_VENDOR "ILL")
set (CPACK_PACKAGE_DESCRIPTION_SUMMARY "Cameo C++ API")
set (CPACK_PACKAGE_DESCRIPTION "This is the long description")
#set (CPACK_PACKAGE_DESCRIPTION_FILE ${CMAKE_SOURCE_DIR}/DESCRIPTION)
set (CPACK_PACKAGE_CONTACT "Shervin Nourbakhsh <shervin.nourbakhsh@ill.fr>")


set (CPACK_RESOURCE_FILE_LICENSE ${CMAKE_SOURCE_DIR}/LICENSE.txt)
set (CPACK_RESOURCE_FILE_README ${CMAKE_SOURCE_DIR}/README.md)
set (CPACK_PACKAGE_CHECKSUM MD5)

set (CPACK_COMPONENTS_IGNORE_GROUPS ON)
set (CPACK_GENERATOR "DEB")
set (CPACK_DEB_COMPONENT_INSTALL ON)

#set (CPACK_MONOLITHIC_INSTALL OFF)
#set (CPACK_DEBIAN_PACKAGE_ARCHITECTURE "amd64")

set (CPACK_COMPONENT_DEV_REQUIRED ON)
set (CPACK_COMPONENT_LIB_REQUIRED ON)
set (CPACK_COMPONENT_DEV_DEPENDS LIB)

set (CPACK_DEBIAN_FILE_NAME DEB-DEFAULT)
set (CPACK_DEBIAN_PACKAGE_GENERATE_SHLIBS ON)
set (CPACK_DEBIAN_PACKAGE_SHLIBDEPS ON)
set (CPACK_DEBIAN_PACKAGE_GENERATE_SHLIBS_POLICY ">=")
#set (CPACK_DEBIAN_ENABLE_COMPONENT_DEPENDS ON)
set (CPACK_DEBIAN_PACKAGE_DEPENDS ON)
set (CPACK_DEBIAN_LIB_PACKAGE_DEPENDS "cameo(>=1.0.0), libzmq (>=5)")
set (CPACK_DEBIAN_LIB_PACKAGE_SECTION lib)
set (CPACK_DEBIAN_DEV_PACKAGE_DEPENDS "${LIBNAME}-lib (=${PROJECT_VERSION}), libzmq3-dev")
set (CPACK_DEBIAN_DEV_PACKAGE_RECOMMENDS "rapidjson-dev (>=5)")
set (CPACK_DEBIAN_DEV_PACKAGE_SECTION libdevel)
set (CPACK_DEBIAN_PACKAGE_PRIORITY optional)


if((${CMAKE_BUILD_TYPE} EQUAL "Debug") OR (${CMAKE_BUILD_TYPE} EQUAL "RelWithDebInfo"))
  set (CPACK_DEBIAN_DEBUGINFO_PACKAGE ON)
endif()



include(CPack)

cpack_add_component(lib
  DISPLAY_NAME "LIBB"
  DESCRIPTION "runtime library"
  REQUIRED #[HIDDEN | REQUIRED | DISABLED ]
  GROUP LIBBB
  #DEPENDS 
  #[INSTALL_TYPES type1 type2 ... ]
  #[DOWNLOADED]
  #[ARCHIVE_FILE filename]
  #[PLIST filename]
  )

cpack_add_component(dev
  DISPLAY_NAME "DEVV"
  DESCRIPTION "library development"
  #REQUIRED #[HIDDEN | REQUIRED | DISABLED ]
  GROUP DEVVV
  DEPENDS lib
  #[INSTALL_TYPES type1 type2 ... ]
  #[DOWNLOADED]
  #[ARCHIVE_FILE filename]
  #[PLIST filename]
  )

#------------------------------------------------------------
# Tests
#------------------------------------------------------------
if(TESTS)
# Note: execute ctest --verbose to have more details 
enable_testing()
add_subdirectory(tests)
endif(TESTS)

